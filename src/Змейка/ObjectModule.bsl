
Процедура Инициализировать() Экспорт
	ВысотаПоля = 20;
	ШиринаПоля = 20;
	Состояние = ВозможныеСостоянияИгры().Играем;
	
	Змейка = Новый Структура;
	Змейка.Вставить("НаправлениеДвижения", ВозможныеНаправленияДвижения().Вверх);
	ТелоЗмейки = Новый Массив;
	СерединаИгровогоПоляX = ШиринаПоля / 2;
	СерединаИгровогоПоляY = ВысотаПоля / 2;
	ТелоЗмейки.Добавить(Координаты(СерединаИгровогоПоляX, СерединаИгровогоПоляY));
	ТелоЗмейки.Добавить(Координаты(СерединаИгровогоПоляX, СерединаИгровогоПоляY + 1));
	Змейка.Вставить("Тело", ТелоЗмейки);
КонецПроцедуры

// { Команды
Процедура ПовернутьНаправо() Экспорт
	ВозможныеНаправленияДвижения = ВозможныеНаправленияДвижения();
	Если Змейка.НаправлениеДвижения = ВозможныеНаправленияДвижения.Вверх Тогда
		Змейка.НаправлениеДвижения = ВозможныеНаправленияДвижения.Направо;
	ИначеЕсли Змейка.НаправлениеДвижения = ВозможныеНаправленияДвижения.Направо Тогда
		Змейка.НаправлениеДвижения = ВозможныеНаправленияДвижения.Вниз;
	ИначеЕсли Змейка.НаправлениеДвижения = ВозможныеНаправленияДвижения.Вниз Тогда
		Змейка.НаправлениеДвижения = ВозможныеНаправленияДвижения.Налево;
	ИначеЕсли Змейка.НаправлениеДвижения = ВозможныеНаправленияДвижения.Налево Тогда
		Змейка.НаправлениеДвижения = ВозможныеНаправленияДвижения.Вверх;
	КонецЕсли;
КонецПроцедуры
// } Команды

Процедура СделатьХод() Экспорт
	ПродвинутьЗмейку();
КонецПроцедуры

Процедура ПродвинутьЗмейку()
	ПродвинутьТелоЗмейкиБезГоловы();
	ПродвинутьГоловуЗмейки();
КонецПроцедуры

Процедура ПродвинутьТелоЗмейкиБезГоловы()
	МаксимальныйИндекс = Змейка.Тело.ВГраница();
	Для Сч = 0 По МаксимальныйИндекс - 1 Цикл
		ТекущаяЧастьЗмейки = Змейка.Тело[МаксимальныйИндекс - Сч];
		ПредыдущаяЧасть = Змейка.Тело[МаксимальныйИндекс - Сч - 1];
		ЗаполнитьЗначенияСвойств(ТекущаяЧастьЗмейки, ПредыдущаяЧасть);
	КонецЦикла;
КонецПроцедуры

Процедура ПродвинутьГоловуЗмейки()
	ВозможныеНаправленияДвижения = ВозможныеНаправленияДвижения();
	ГоловаЗмейки = Змейка.Тело[0];
	Если Змейка.НаправлениеДвижения = ВозможныеНаправленияДвижения.Вверх Тогда
		ГоловаЗмейки.Y = ГоловаЗмейки.Y - 1;
	ИначеЕсли Змейка.НаправлениеДвижения = ВозможныеНаправленияДвижения.Направо Тогда
		ГоловаЗмейки.X = ГоловаЗмейки.X + 1;
	ИначеЕсли Змейка.НаправлениеДвижения = ВозможныеНаправленияДвижения.Вниз Тогда
		ГоловаЗмейки.Y = ГоловаЗмейки.Y + 1;
	ИначеЕсли Змейка.НаправлениеДвижения = ВозможныеНаправленияДвижения.Налево Тогда
		ГоловаЗмейки.X = ГоловаЗмейки.X - 1;
	КонецЕсли;
КонецПроцедуры

// { Helpers
Функция ВозможныеСостоянияИгры() Экспорт
	СостоянияИгры = Новый Структура;
	СостоянияИгры.Вставить("Играем", "Играем");
	
	Возврат Новый ФиксированнаяСтруктура(СостоянияИгры);
КонецФункции

Функция ВозможныеНаправленияДвижения() Экспорт
	НаправленияДвижения = Новый Структура;
	НаправленияДвижения.Вставить("Вверх", "Вверх");
	НаправленияДвижения.Вставить("Направо", "Направо");
	НаправленияДвижения.Вставить("Вниз", "Вниз");
	НаправленияДвижения.Вставить("Налево", "Налево");
	
	Возврат Новый ФиксированнаяСтруктура(НаправленияДвижения);
КонецФункции

Функция Координаты(X, Y)
	Возврат Новый Структура("X, Y", X, Y);
КонецФункции
// } Helpers
