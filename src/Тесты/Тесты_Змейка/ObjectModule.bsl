Перем КонтекстЯдра;
Перем Ожидаем;
Перем Игра;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестов) Экспорт
	НаборТестов.Добавить("ТестДолжен_СоздатьИгру");
	НаборТестов.Добавить("ТестДолжен_ИнициализироватьИгру");
	НаборТестов.Добавить("ТестДолжен_ПроверитьПоворотыЗмейкиПоЧасовойСтрелке");
	НаборТестов.Добавить("ТестДолжен_ПродвинутьЗмейкуВверх");
	НаборТестов.Добавить("ТестДолжен_ПродвинутьЗмейкуНаправо");
	НаборТестов.Добавить("ТестДолжен_ПродвинутьЗмейкуВниз");
	НаборТестов.Добавить("ТестДолжен_ПродвинутьЗмейкуНалево");
КонецПроцедуры

Процедура ПередЗапускомТеста() Экспорт
	ПутьКИгре = СтрЗаменить(ЭтотОбъект.ИспользуемоеИмяФайла, "Тесты\Тесты_", "");
	Игра = ВнешниеОбработки.Создать(ПутьКИгре);
	Игра.Инициализировать();
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	
КонецПроцедуры

Процедура ТестДолжен_СоздатьИгру() Экспорт
	// Проверка
	Ожидаем.Что(Игра, "Игра существует").Существует();
КонецПроцедуры

Процедура ТестДолжен_ИнициализироватьИгру() Экспорт
	// Проверка
	Ожидаем.Что(Игра.ВысотаПоля, "Игра.ВысотаПоля").Равно(20);
	Ожидаем.Что(Игра.ШиринаПоля, "Игра.ШиринаПоля").Равно(20);
	Ожидаем.Что(Игра.Состояние, "Игра.Состояние").Равно(Игра.ВозможныеСостоянияИгры().Играем);
	Змейка = Игра.Змейка;
	Ожидаем.Что(Змейка, "Змейка это структура").ИмеетТип("Структура");
	Ожидаем.Что(Змейка.НаправлениеДвижения, "Змейка.НаправлениеДвижения").Равно(Игра.ВозможныеНаправленияДвижения().Вверх);
	Тело = Змейка.Тело;
	Ожидаем.Что(Тело, "Тело змейки").ИмеетТип("Массив").ИмеетДлину(2);
	ГоловаЗмейки = Тело[0];
	Ожидаем.Что(ГоловаЗмейки.X, "ГоловаЗмейки.X").Равно(Игра.ШиринаПоля / 2);
	Ожидаем.Что(ГоловаЗмейки.Y, "ГоловаЗмейки.Y").Равно(Игра.ВысотаПоля / 2);
	
	ВтораяЧастьЗмейки = Тело[1];
	Ожидаем.Что(ВтораяЧастьЗмейки.X, "ВтораяЧастьЗмейки.X").Равно(Игра.ШиринаПоля / 2);
	Ожидаем.Что(ВтораяЧастьЗмейки.Y, "ВтораяЧастьЗмейки.Y").Равно(Игра.ВысотаПоля / 2 + 1);
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПоворотыЗмейкиПоЧасовойСтрелке() Экспорт
	// Действие
	Игра.ПовернутьНаправо();
	Змейка = Игра.Змейка;
	
	// Проверка
	Ожидаем.Что(Змейка.НаправлениеДвижения, "Змейка.НаправлениеДвижения.Направо").Равно(Игра.ВозможныеНаправленияДвижения().Направо);
	
	Игра.ПовернутьНаправо();
	Ожидаем.Что(Змейка.НаправлениеДвижения, "Змейка.НаправлениеДвижения.Вниз").Равно(Игра.ВозможныеНаправленияДвижения().Вниз);
	
	Игра.ПовернутьНаправо();
	Ожидаем.Что(Змейка.НаправлениеДвижения, "Змейка.НаправлениеДвижения.Налево").Равно(Игра.ВозможныеНаправленияДвижения().Налево);
	
	Игра.ПовернутьНаправо();
	Ожидаем.Что(Змейка.НаправлениеДвижения, "Змейка.НаправлениеДвижения.Вверх").Равно(Игра.ВозможныеНаправленияДвижения().Вверх);
КонецПроцедуры

Процедура ТестДолжен_ПродвинутьЗмейкуВверх() Экспорт
	// Подготовка
	НачальноеПоложениеГоловыЗмейки = Новый ФиксированнаяСтруктура(Игра.Змейка.Тело[0]);
	
	// Действие
	Игра.СделатьХод();
	
	// Проверка
	ТелоЗмейки = Игра.Змейка.Тело;
	ГоловаЗмейки = ТелоЗмейки[0];
	Ожидаем.Что(ГоловаЗмейки.X, "Новое положение ГоловаЗмейки.X").Равно(НачальноеПоложениеГоловыЗмейки.X);
	Ожидаем.Что(ГоловаЗмейки.Y, "Новое положение ГоловаЗмейки.Y").Равно(НачальноеПоложениеГоловыЗмейки.Y - 1);
	
	ВтораяЧастьЗмейки = ТелоЗмейки[1];
	Ожидаем.Что(ВтораяЧастьЗмейки.X, "Новое положение ВтораяЧастьЗмейки.X").Равно(НачальноеПоложениеГоловыЗмейки.X);
	Ожидаем.Что(ВтораяЧастьЗмейки.Y, "Новое положение ВтораяЧастьЗмейки.Y").Равно(НачальноеПоложениеГоловыЗмейки.Y);
КонецПроцедуры

Процедура ТестДолжен_ПродвинутьЗмейкуНаправо() Экспорт
	// Подготовка
	НачальноеПоложениеГоловыЗмейки = Новый ФиксированнаяСтруктура(Игра.Змейка.Тело[0]);
	Игра.ПовернутьНаправо();
	
	// Действие
	Игра.СделатьХод();
	
	// Проверка
	ТелоЗмейки = Игра.Змейка.Тело;
	ГоловаЗмейки = ТелоЗмейки[0];
	Ожидаем.Что(ГоловаЗмейки.X, "Новое положение ГоловаЗмейки.X").Равно(НачальноеПоложениеГоловыЗмейки.X + 1);
	Ожидаем.Что(ГоловаЗмейки.Y, "Новое положение ГоловаЗмейки.Y").Равно(НачальноеПоложениеГоловыЗмейки.Y);
	
	ВтораяЧастьЗмейки = ТелоЗмейки[1];
	Ожидаем.Что(ВтораяЧастьЗмейки.X, "Новое положение ВтораяЧастьЗмейки.X").Равно(НачальноеПоложениеГоловыЗмейки.X);
	Ожидаем.Что(ВтораяЧастьЗмейки.Y, "Новое положение ВтораяЧастьЗмейки.Y").Равно(НачальноеПоложениеГоловыЗмейки.Y);
КонецПроцедуры

Процедура ТестДолжен_ПродвинутьЗмейкуВниз() Экспорт
	// Подготовка
	Игра.ПовернутьНаправо();
	Игра.СделатьХод();
	Игра.ПовернутьНаправо();
	НачальноеПоложениеГоловыЗмейки = Новый ФиксированнаяСтруктура(Игра.Змейка.Тело[0]);
	
	// Действие
	Игра.СделатьХод();
	
	// Проверка
	ТелоЗмейки = Игра.Змейка.Тело;
	ГоловаЗмейки = ТелоЗмейки[0];
	Ожидаем.Что(ГоловаЗмейки.X, "Новое положение ГоловаЗмейки.X").Равно(НачальноеПоложениеГоловыЗмейки.X);
	Ожидаем.Что(ГоловаЗмейки.Y, "Новое положение ГоловаЗмейки.Y").Равно(НачальноеПоложениеГоловыЗмейки.Y + 1);
	
	ВтораяЧастьЗмейки = ТелоЗмейки[1];
	Ожидаем.Что(ВтораяЧастьЗмейки.X, "Новое положение ВтораяЧастьЗмейки.X").Равно(НачальноеПоложениеГоловыЗмейки.X);
	Ожидаем.Что(ВтораяЧастьЗмейки.Y, "Новое положение ВтораяЧастьЗмейки.Y").Равно(НачальноеПоложениеГоловыЗмейки.Y);
КонецПроцедуры

Процедура ТестДолжен_ПродвинутьЗмейкуНалево() Экспорт
	// Подготовка
	Игра.ПовернутьНаправо();
	Игра.ПовернутьНаправо();
	Игра.ПовернутьНаправо();
	НачальноеПоложениеГоловыЗмейки = Новый ФиксированнаяСтруктура(Игра.Змейка.Тело[0]);
	
	// Действие
	Игра.СделатьХод();
	
	// Проверка
	ТелоЗмейки = Игра.Змейка.Тело;
	ГоловаЗмейки = ТелоЗмейки[0];
	Ожидаем.Что(ГоловаЗмейки.X, "Новое положение ГоловаЗмейки.X").Равно(НачальноеПоложениеГоловыЗмейки.X - 1);
	Ожидаем.Что(ГоловаЗмейки.Y, "Новое положение ГоловаЗмейки.Y").Равно(НачальноеПоложениеГоловыЗмейки.Y);
	
	ВтораяЧастьЗмейки = ТелоЗмейки[1];
	Ожидаем.Что(ВтораяЧастьЗмейки.X, "Новое положение ВтораяЧастьЗмейки.X").Равно(НачальноеПоложениеГоловыЗмейки.X);
	Ожидаем.Что(ВтораяЧастьЗмейки.Y, "Новое положение ВтораяЧастьЗмейки.Y").Равно(НачальноеПоложениеГоловыЗмейки.Y);
КонецПроцедуры
